! ------------ module COMPLEX_GEOMETRY

MODULE COMPLEX_GEOMETRY ! this module will be moved to FDS

USE PRECISION_PARAMETERS
USE COMP_FUNCTIONS, ONLY: CHECKREAD,SHUTDOWN
USE MEMORY_FUNCTIONS, ONLY: ChkMemErr
USE GLOBAL_CONSTANTS
USE TYPES

IMPLICIT NONE

PRIVATE
PUBLIC :: READ_GEOM,WRITE_GEOM,WRITE_SMV
 
CONTAINS

SUBROUTINE READ_GEOM

INTEGER, PARAMETER :: MAX_VERTS=10000000 ! at some point we may decide to use dynmaic memory allocation
INTEGER, PARAMETER :: MAX_FACES=MAX_VERTS
CHARACTER(30) :: ID,SURF_ID
REAL(EB) :: VERTS(3*MAX_VERTS)
INTEGER :: FACES(3*MAX_FACES)
INTEGER :: N_VERTS, N_FACES
INTEGER :: IOS,IZERO,N
TYPE(GEOMETRY_TYPE), POINTER :: G=>NULL()
NAMELIST /GEOM/ FACES, ID, N_FACES, N_VERTS, SURF_ID, VERTS

N_GEOM=0
REWIND(LU_INPUT)
COUNT_GEOM_LOOP: DO
   CALL CHECKREAD('GEOM',LU_INPUT,IOS)
   IF (IOS==1) EXIT COUNT_GEOM_LOOP
   READ(LU_INPUT,NML=GEOM,END=11,ERR=12,IOSTAT=IOS)
   N_GEOM=N_GEOM+1
   12 IF (IOS>0) CALL SHUTDOWN('ERROR: problem with GEOM line')
ENDDO COUNT_GEOM_LOOP
11 REWIND(LU_INPUT)

IF (N_GEOM==0) RETURN

! Allocate GEOMETRY array

ALLOCATE(GEOMETRY(N_GEOM),STAT=IZERO)
CALL ChkMemErr('READ','GEOMETRY',IZERO)

READ_GEOM_LOOP: DO N=1,N_GEOM
   G=>GEOMETRY(N)
   
   CALL CHECKREAD('GEOM',LU_INPUT,IOS)
   IF (IOS==1) EXIT READ_GEOM_LOOP
   
   ! Set defaults
   
   ID = 'geom'
   N_FACES=0
   N_VERTS=0
   SURF_ID = 'INERT'
   VERTS=0.0
   FACES=0

   ! Read the GEOM line
   
   READ(LU_INPUT,GEOM,END=35)
   
   G%ID = ID
   G%N_FACES = N_FACES
   G%N_VERTS = N_VERTS
   G%SURF_ID = SURF_ID

   IF (N_FACES.GT.0.AND.N_FACES.LE.MAX_FACES) THEN
      ALLOCATE(G%FACES(3*N_FACES),STAT=IZERO)
      CALL ChkMemErr('READ_GEOM','FACES',IZERO)
   ELSE
      CALL SHUTDOWN('ERROR: problem with GEOM line, too many faces')
   ENDIF
   IF (N_VERTS.GT.0.AND.N_VERTS.LE.MAX_VERTS) THEN
      ALLOCATE(G%VERTS(3*N_VERTS),STAT=IZERO)
      CALL ChkMemErr('READ_GEOM','VERTS',IZERO)
   ELSE
      CALL SHUTDOWN('ERROR: problem with GEOM line, too many vertices')
   ENDIF
   G%VERTS(1:3*N_VERTS) = VERTS(1:3*N_VERTS)
   G%FACES(1:3*N_FACES) = FACES(1:3*N_FACES)
   
ENDDO READ_GEOM_LOOP
35 REWIND(LU_INPUT)

END SUBROUTINE READ_GEOM

SUBROUTINE MERGE_GEOMS(VERTS,N_VERTS,FACES,N_FACES)
   INTEGER N_VERTS, N_FACES, I, J
   INTEGER, DIMENSION(:) :: FACES(3*N_FACES)
   INTEGER, DIMENSION(:) :: OFFSETS(0:N_GEOM)
   REAL(EB), DIMENSION(:) :: VERTS(3*N_VERTS)
   TYPE(GEOMETRY_TYPE), POINTER :: G=>NULL()
   INTEGER :: IZERO
   
   OFFSETS(0)=0
   DO I = 1, N_GEOM
      G=>GEOMETRY(I)
      
      OFFSETS(I) = OFFSETS(I-1) + G%N_VERTS
   END DO
   DO I = 0, N_GEOM-1
      G=>GEOMETRY(I+1)
      DO J = 0, G%N_VERTS-1
         VERTS(3*I+3*J+1) = G%VERTS(3*J+1)
         VERTS(3*I+3*J+2) = G%VERTS(3*J+2)
         VERTS(3*I+3*J+3) = G%VERTS(3*J+3)
      END DO
      DO J = 0, G%N_FACES-1
         FACES(3*I+3*J+1) = G%FACES(3*J+1)+OFFSETS(I)
         FACES(3*I+3*J+2) = G%FACES(3*J+2)+OFFSETS(I)
         FACES(3*I+3*J+3) = G%FACES(3*J+3)+OFFSETS(I)
      END DO
   END DO
END SUBROUTINE MERGE_GEOMS

SUBROUTINE WRITE_GEOM
INTEGER :: I
TYPE(GEOMETRY_TYPE), POINTER :: G=>NULL()
INTEGER :: N_VERTS, N_FACES
INTEGER, ALLOCATABLE, DIMENSION(:) :: FACES
REAL(EB), ALLOCATABLE, DIMENSION(:) :: VERTS
INTEGER :: IZERO
INTEGER :: ONE=1, ZERO=0, VERSION=0
REAL(FB) :: STIME=0.0
INTEGER :: N_VERT_S_VALS, N_VERT_D_VALS
INTEGER :: N_FACE_S_VALS, N_FACE_D_VALS

IF (N_GEOM.LE.0) RETURN
N_VERTS=0
N_FACES=0
DO I = 1, N_GEOM
   G=>GEOMETRY(I)
      
   N_VERTS = N_VERTS + G%N_VERTS
   N_FACES = N_FACES + G%N_FACES
END DO
IF(N_VERTS.LE.0.OR.N_VERTS.LE.0)RETURN

ALLOCATE(VERTS(3*N_VERTS),STAT=IZERO)
CALL ChkMemErr('WRITE_GEOM_TO_SMV','VERTS',IZERO)
   
ALLOCATE(FACES(3*N_FACES),STAT=IZERO)
CALL ChkMemErr('WRITE_GEOM_TO_SMV','FACES',IZERO)

CALL MERGE_GEOMS(VERTS,N_VERTS,FACES,N_FACES)

OPEN(LU_GEOM(1),FILE=FN_GEOM(1),FORM='UNFORMATTED',STATUS='REPLACE')

N_VERT_S_VALS=N_VERTS
N_VERT_D_VALS=0
N_FACE_S_VALS = N_FACES
N_FACE_D_VALS=0

WRITE(LU_GEOM(1)) ONE
WRITE(LU_GEOM(1)) VERSION
WRITE(LU_GEOM(1)) ZERO ! floating point header
WRITE(LU_GEOM(1)) ZERO ! integer header
WRITE(LU_GEOM(1)) STIME,ZERO
WRITE(LU_GEOM(1)) N_VERT_S_VALS,N_FACE_S_VALS
IF (N_VERT_S_VALS>0) WRITE(LU_GEOM(1)) (REAL(VERTS(I),FB), I=1,3*N_VERT_S_VALS)
IF (N_FACE_S_VALS>0) THEN
   WRITE(LU_GEOM(1)) (FACES(I), I=1,3*N_FACE_S_VALS)
   WRITE(LU_GEOM(1)) (ZERO, I=1,N_FACE_S_VALS)
ENDIF

END SUBROUTINE WRITE_GEOM

SUBROUTINE WRITE_SMV

OPEN(LU_SMV,FILE=FN_SMV)

WRITE(LU_SMV,'(/A)') 'CHID'
WRITE(LU_SMV,'(1X,A)') TRIM(CHID)

WRITE(LU_SMV,'(/A)') 'GEOM'
WRITE(LU_SMV,'(1X,A)') FN_GEOM(1)

END SUBROUTINE WRITE_SMV



END MODULE COMPLEX_GEOMETRY
