! VVVVV placeholder routines VVVVV

MODULE PRECISION_PARAMETERS
 
! Set important parameters having to do with variable precision and array allocations
 
IMPLICIT NONE
 
! Precision of "Four Byte" and "Eight Byte" reals

INTEGER, PARAMETER :: FB = SELECTED_REAL_KIND(6)
INTEGER, PARAMETER :: EB = SELECTED_REAL_KIND(12)
END MODULE PRECISION_PARAMETERS

MODULE TYPES
USE PRECISION_PARAMETERS

TYPE GEOMETRY_TYPE
   CHARACTER(30) :: ID='geom'
   CHARACTER(30) :: SURF_ID='null'
   INTEGER :: N_VERTS, N_FACES
   INTEGER, ALLOCATABLE, DIMENSION(:) :: FACES
   REAL(EB), ALLOCATABLE, DIMENSION(:) :: VERTS
END TYPE GEOMETRY_TYPE
TYPE(GEOMETRY_TYPE), ALLOCATABLE, TARGET, DIMENSION(:) :: GEOMETRY
END MODULE TYPES

MODULE COMPLEX_GEOMETRY

USE PRECISION_PARAMETERS
USE TYPES

IMPLICIT NONE
INTEGER :: N_GEOM

PRIVATE
PUBLIC :: READ_GEOM,WRITE_GEOM
 
CONTAINS

SUBROUTINE ChkMemErr(CodeSect,VarName,IZERO)
 
! Memory checking routine
 
CHARACTER(*), INTENT(IN) :: CodeSect, VarName
INTEGER IZERO
CHARACTER(100) MESSAGE
 
IF (IZERO==0) RETURN
 
WRITE(MESSAGE,'(4A)') 'ERROR: Memory allocation failed for ', TRIM(VarName),' in the routine ',TRIM(CodeSect)
CALL SHUTDOWN(MESSAGE)

END SUBROUTINE ChkMemErr

SUBROUTINE SHUTDOWN(MESSAGE)  
CHARACTER(*), INTENT(IN) :: MESSAGE

WRITE(6,'(/A)') TRIM(MESSAGE)

STOP

END SUBROUTINE SHUTDOWN

SUBROUTINE CHECKREAD(NAME,LU,IOS)

! Look for the namelist variable NAME and then stop at that line.

INTEGER :: II
INTEGER, INTENT(OUT) :: IOS
INTEGER, INTENT(IN) :: LU
CHARACTER(4), INTENT(IN) :: NAME
CHARACTER(80) TEXT
IOS = 1

READLOOP: DO
   READ(LU,'(A)',END=10) TEXT
   TLOOP: DO II=1,72
      IF (TEXT(II:II)/='&' .AND. TEXT(II:II)/=' ') EXIT TLOOP
      IF (TEXT(II:II)=='&') THEN
         IF (TEXT(II+1:II+4)==NAME) THEN
            BACKSPACE(LU)
            IOS = 0
            EXIT READLOOP
         ELSE
            CYCLE READLOOP
         ENDIF
      ENDIF
   ENDDO TLOOP
ENDDO READLOOP
 
10 RETURN
END SUBROUTINE CHECKREAD

! ^^^^ placeholder routines ^^^^^^^

SUBROUTINE READ_GEOM

INTEGER, PARAMETER :: MAX_VERTS=10000000 ! at some point we may decide to use dynmaic memory allocation
INTEGER, PARAMETER :: MAX_FACES=MAX_VERTS
CHARACTER(30) :: ID,SURF_ID
REAL(EB) :: VERTS(3*MAX_VERTS)
INTEGER :: FACES(3*MAX_FACES)
INTEGER :: N_VERTS, N_FACES
INTEGER :: IOS,IZERO,N
TYPE(GEOMETRY_TYPE), POINTER :: G=>NULL()
NAMELIST /GEOM/ FACES, ID, N_FACES, N_VERTS, SURF_ID, VERTS

INTEGER :: LU_INPUT

N_GEOM=0
REWIND(LU_INPUT)
COUNT_GEOM_LOOP: DO
   CALL CHECKREAD('GEOM',LU_INPUT,IOS)
   IF (IOS==1) EXIT COUNT_GEOM_LOOP
   READ(LU_INPUT,NML=GEOM,END=11,ERR=12,IOSTAT=IOS)
   N_GEOM=N_GEOM+1
   12 IF (IOS>0) CALL SHUTDOWN('ERROR: problem with GEOM line')
ENDDO COUNT_GEOM_LOOP
11 REWIND(LU_INPUT)

IF (N_GEOM==0) RETURN

! Allocate GEOMETRY array

ALLOCATE(GEOMETRY(N_GEOM),STAT=IZERO)
CALL ChkMemErr('READ','GEOMETRY',IZERO)

READ_GEOM_LOOP: DO N=1,N_GEOM
   G=>GEOMETRY(N)
   
   CALL CHECKREAD('GEOM',LU_INPUT,IOS)
   IF (IOS==1) EXIT READ_GEOM_LOOP
   
   ! Set defaults
   
   ID = 'geom'
   N_FACES=0
   N_VERTS=0
   SURF_ID = 'INERT'

   ! Read the GEOM line
   
   READ(LU_INPUT,GEOM,END=35)
   
   G%ID = ID
   G%N_FACES = N_FACES
   G%N_VERTS = N_VERTS
   G%SURF_ID = SURF_ID

   IF (N_FACES.GT.0.AND.N_FACES.LE.MAX_FACES) THEN
      ALLOCATE(G%FACES(3*N_FACES),STAT=IZERO)
      CALL ChkMemErr('READ_GEOM','FACES',IZERO)
   ELSE
      CALL SHUTDOWN('ERROR: problem with GEOM line, too many faces')
   ENDIF
   IF (N_VERTS.GT.0.AND.N_VERTS.LE.MAX_VERTS) THEN
      ALLOCATE(G%VERTS(3*N_VERTS),STAT=IZERO)
      CALL ChkMemErr('READ_GEOM','VERTS',IZERO)
   ELSE
      CALL SHUTDOWN('ERROR: problem with GEOM line, too many vertices')
   ENDIF
   G%VERTS(1:3*N_VERTS) = VERTS(1:3*N_VERTS)
   G%FACES(1:3*N_FACES) = FACES(1:3*N_FACES)
   
ENDDO READ_GEOM_LOOP
35 REWIND(LU_INPUT)

END SUBROUTINE READ_GEOM

subroutine write_geom
integer :: i
TYPE(GEOMETRY_TYPE), POINTER :: G=>NULL()

write(6,*)"NGEOM=",N_GEOM
do i = 1, N_GEOM
   G=>GEOMETRY(i)
   write(6,*)"geom=",i,"nverts=",g%N_VERTS,"nfaces=",g%N_FACES
end do
end subroutine write_geom



END MODULE COMPLEX_GEOMETRY
